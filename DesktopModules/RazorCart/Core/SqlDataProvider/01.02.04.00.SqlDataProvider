/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/



/************************************************************/
/*****               Create Tables                      *****/
/************************************************************/



/************************************************************/
/*****                  Alter Tables                    *****/
/************************************************************/
IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}Smith_ProductDiscountsByRole') AND type in (N'U'))
BEGIN
	IF NOT EXISTS (SELECT * FROM sys.columns WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}Smith_ProductDiscountsByRole') AND name = N'StartDate')
	BEGIN
		ALTER TABLE
			{databaseOwner}{objectQualifier}Smith_ProductDiscountsByRole
		ADD
			[StartDate] [DATETIME]
	END
	IF NOT EXISTS (SELECT * FROM sys.columns WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}Smith_ProductDiscountsByRole') AND name = N'EndDate')
	BEGIN
		ALTER TABLE
			{databaseOwner}{objectQualifier}Smith_ProductDiscountsByRole
		ADD
			[EndDate] [DATETIME]
	END
END
GO



/************************************************************/
/*****                Drop Sprocs                       *****/
/************************************************************/
IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}RZC_CreateProductRoleDiscount') AND type in (N'P', N'PC'))
	DROP PROCEDURE {databaseOwner}{objectQualifier}RZC_CreateProductRoleDiscount
GO
IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}RZC_UpdateProductRoleDiscount') AND type in (N'P', N'PC'))
	DROP PROCEDURE {databaseOwner}{objectQualifier}RZC_UpdateProductRoleDiscount
GO
IF EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}RZC_ListOrdersByFilter') AND type in (N'P', N'PC'))
	DROP PROCEDURE {databaseOwner}{objectQualifier}RZC_ListOrdersByFilter
GO



/************************************************************/
/*****               Create Sprocs                      *****/
/************************************************************/
CREATE PROCEDURE {databaseOwner}{objectQualifier}RZC_CreateProductRoleDiscount
	@ProductID INT = NULL,
	@RoleID INT = NULL,
	@DiscountPercent DECIMAL(6,2) = NULL,
	@DiscountAmount MONEY = NULL,
	@UserName NVARCHAR(100) = NULL,
	@StartDate DATETIME = NULL,
	@EndDate DATETIME = NULL,
	@PortalID INT = NULL
AS
BEGIN
	INSERT INTO
		{databaseOwner}{objectQualifier}Smith_ProductDiscountsByRole
		([ProductID]
		,[RoleID]
		,[DiscountPercent]
		,[DiscountAmount]
		,[UserName]
		,[StartDate]
		,[EndDate]
		,[PortalID])
	VALUES
		(@ProductID
		,@RoleID
		,@DiscountPercent
		,@DiscountAmount
		,@UserName
		,@StartDate
		,@EndDate
		,@PortalID)
	SELECT SCOPE_IDENTITY()
END
GO
-------------------------------------------------------------------------------------------------------------------------------------
CREATE PROCEDURE {databaseOwner}{objectQualifier}RZC_UpdateProductRoleDiscount
	@DiscountID INT = NULL,
	@ProductID INT = NULL,
	@RoleID INT = NULL,
	@DiscountPercent DECIMAL(6,2) = NULL,
	@DiscountAmount MONEY = NULL,
	@UserName NVARCHAR(100) = NULL,
	@StartDate DATETIME = NULL,
	@EndDate DATETIME = NULL,
	@PortalID INT = NULL
AS
BEGIN
	UPDATE
		{databaseOwner}{objectQualifier}Smith_ProductDiscountsByRole
	SET
		[ProductID] = @ProductID,
		[RoleID] = @RoleID,
		[DiscountPercent] = @DiscountPercent,
		[DiscountAmount] = @DiscountAmount,
		[UserName] = @UserName,
		[StartDate] = @StartDate,
		[EndDate] = @EndDate,
		[PortalID] = @PortalID
	WHERE
		[DiscountID] = @DiscountID
END
GO
-------------------------------------------------------------------------------------------------------------------------------------
CREATE PROCEDURE {databaseOwner}{objectQualifier}RZC_ListOrdersByFilter
	@PageNumber INT = NULL,
	@PageSize INT = NULL,
	@OrderColumn VARCHAR(128) = NULL,
	@OrderDirection VARCHAR(4) = NULL,
	@SearchValue NVARCHAR(256) = NULL,
	@From DATETIME = NULL,
	@To DATETIME = NULL,
	@StoreID INT = NULL,
	@PortalID INT = NULL
AS
BEGIN
	SET NOCOUNT ON;
	DECLARE @PageRecordStart INT
	DECLARE @PageRecordEnd INT

	SET @PageRecordStart = ((@PageNumber - 1) * @PageSize) + 1
	SET @PageRecordEnd = @PageNumber * @PageSize

	;WITH [OrdersPage] AS
	(
		SELECT
			(
				CASE
					WHEN @OrderColumn = 'Status' AND @OrderDirection = 'ASC' THEN ROW_NUMBER() OVER (ORDER BY O.[Status] ASC)
					WHEN @OrderColumn = 'Status' AND @OrderDirection = 'DESC' THEN ROW_NUMBER() OVER (ORDER BY O.[Status] DESC)
					WHEN @OrderColumn = 'OrderID' AND @OrderDirection = 'ASC' THEN ROW_NUMBER() OVER (ORDER BY O.[OrderID] ASC)
					WHEN @OrderColumn = 'OrderID' AND @OrderDirection = 'DESC' THEN ROW_NUMBER() OVER (ORDER BY O.[OrderID] DESC)
					WHEN @OrderColumn = 'OrderDate' AND @OrderDirection = 'ASC' THEN ROW_NUMBER() OVER (ORDER BY O.[OrderDate] ASC)
					WHEN @OrderColumn = 'OrderDate' AND @OrderDirection = 'DESC' THEN ROW_NUMBER() OVER (ORDER BY O.[OrderDate] DESC)
					WHEN @OrderColumn = 'GrandTotal' AND @OrderDirection = 'ASC' THEN ROW_NUMBER() OVER (ORDER BY O.[GrandTotal] ASC)
					WHEN @OrderColumn = 'GrandTotal' AND @OrderDirection = 'DESC' THEN ROW_NUMBER() OVER (ORDER BY O.[GrandTotal] DESC)
				ELSE ROW_NUMBER() OVER (ORDER BY O.[OrderID] ASC)
				END
			) AS [RowNo], O.*, P.[PaidTotal]
		FROM
			{databaseOwner}{objectQualifier}Smith_StoreOrders O
		OUTER APPLY
		(
			SELECT
				ISNULL(SUM(PH.[Amount]), 0) AS 'PaidTotal'
			FROM
				{databaseOwner}{objectQualifier}Smith_PayHist PH
			WHERE
				PH.[OrderID] = O.[OrderID]
				AND PH.[Success] = 1
		) P
		WHERE
			(O.[StoreID] = @StoreID OR @StoreID = -1)
			AND O.[PortalID] = @PortalID
			AND (
				O.[ShipFirstName] LIKE '%' + @SearchValue + '%'
				OR O.[ShipLastName] LIKE '%' + @SearchValue + '%'
				OR O.[ShipAddress1] LIKE '%' + @SearchValue + '%'
				OR O.[ShipAddress2] LIKE '%' + @SearchValue + '%'
				OR O.[ShipCity] LIKE '%' + @SearchValue + '%'
				OR O.[ShipZipcode] LIKE '%' + @SearchValue + '%'
				OR O.[ShipMethod] LIKE '%' + @SearchValue + '%'
				OR O.[SpecialInstructions] LIKE '%' + @SearchValue + '%'
				OR O.[Status] LIKE '%' + @SearchValue + '%'
				OR O.[Status] IN (SELECT * FROM {databaseOwner}{objectQualifier}RZC_FxSplit(@SearchValue, ','))
				OR (
					'Unpaid' IN (SELECT * FROM {databaseOwner}{objectQualifier}RZC_FxSplit(@SearchValue, ','))
					AND P.[PaidTotal] < O.[GrandTotal]
				)
			)
			AND (
				CONVERT(DATE, O.[OrderDate]) BETWEEN ISNULL(@From, CAST(-53690 AS DATETIME)) AND ISNULL(@To, GETDATE())
			)
	)
	SELECT
		*,
		TotalRecords = (
			SELECT COUNT(*) FROM [OrdersPage]
		),
		MoreRecords = (
			SELECT COUNT(*) FROM [OrdersPage]
			WHERE [RowNo] > @PageRecordEnd
		)
	FROM
		[OrdersPage]
	WHERE
		[RowNo] BETWEEN @PageRecordStart AND @PageRecordEnd
	ORDER BY
		[RowNo];
END
GO



/************************************************************/
/*****               End of Script                      *****/
/************************************************************/
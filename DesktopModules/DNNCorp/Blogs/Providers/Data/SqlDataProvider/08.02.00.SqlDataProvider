IF OBJECT_ID(N'{databaseOwner}[{objectQualifier}Blogs_Blog_GetRoster_Paged]', N'P') IS NULL
	EXEC('CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Blog_GetRoster_Paged] AS BEGIN SELECT 1 END');
GO

ALTER PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Blog_GetRoster_Paged]
	@ModuleId INT,
    @GroupId INT,
	@PortalId INT,
	@ShowUnauthorized BIT,
	@ShowOnlyWithEntries BIT,
	@PageSize INT,
	@PageIndex INT
AS
BEGIN
	SET @GroupId = COALESCE(@GroupId, -1)

	;WITH OrderedSet AS
	(
		SELECT *, 
			ROW_NUMBER() OVER ( ORDER BY Title ASC ) AS RowNumber ,
			COUNT(*) OVER () AS [TotalRecords]
		FROM
		(
			SELECT  B.* ,
					( SELECT    COUNT(BlogId)
					  FROM      {databaseOwner}[{objectQualifier}Blogs_Entry]
					  WHERE     BlogId = B.BlogId
								AND Approved = 1
								AND PublishOnDate <= GETUTCDATE()
					) AS BlogPostCount
			FROM    {databaseOwner}[{objectQualifier}Blogs_Blog] B
			WHERE   B.PortalId = @PortalId
				AND	B.BlogId IN (
						SELECT BB.BlogId FROM  {databaseOwner}[{objectQualifier}Blogs_Blog] BB
							LEFT JOIN {databaseOwner}[{objectQualifier}Blogs_Entry] E ON B.BlogId = E.BlogId
							LEFT JOIN {databaseOwner}[{objectQualifier}ContentItems] CI ON E.ContentItemId = CI.ContentItemID
						WHERE (( CI.ModuleId = @ModuleId ) OR ( CI.ModuleId IS NULL ))
						  AND (( @GroupId < 1 AND (BB.GroupId < 1 OR BB.GroupId IS NULL)) OR ( BB.GroupId = @GroupId ))
					) 
				AND (@ShowUnauthorized = 1 OR (@ShowUnauthorized = 0 AND B.Authorized = 1))
		) AS Query
		WHERE (@ShowOnlyWithEntries = 0 OR (@ShowOnlyWithEntries = 1 AND BlogPostCount > 0))
	)
	SELECT *
	FROM OrderedSet
	WHERE RowNumber BETWEEN ( @PageIndex * @PageSize + 1 ) AND ( ( @PageIndex + 1 ) * @PageSize )
	ORDER BY RowNumber ASC
END
GO

IF OBJECT_ID(N'{databaseOwner}[{objectQualifier}Blogs_Blog_GetRoster]', N'P') IS NULL
	EXEC('CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Blog_GetRoster] AS BEGIN SELECT 1 END');
GO

ALTER PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Blog_GetRoster]
    @ModuleId INT,
    @GroupId INT,
	@PortalId INT,
	@ShowUnauthorized BIT
AS
BEGIN
	SELECT @GroupId = Coalesce(@GroupId, -1);

    SELECT  B.* ,
            ( SELECT    COUNT(BlogId)
              FROM      {databaseOwner}[{objectQualifier}Blogs_Entry]
              WHERE     BlogId = B.BlogId
                        AND Approved = 1
                        AND PublishOnDate <= GETUTCDATE()
            ) AS BlogPostCount
    FROM    {databaseOwner}[{objectQualifier}Blogs_Blog] B
    WHERE   B.PortalId = @PortalId
	  AND	B.BlogId IN (
				SELECT BB.BlogId FROM  {databaseOwner}[{objectQualifier}Blogs_Blog] BB
				LEFT JOIN {databaseOwner}[{objectQualifier}Blogs_Entry] E ON B.BlogId = E.BlogId
				LEFT JOIN {databaseOwner}[{objectQualifier}ContentItems] CI ON E.ContentItemId = CI.ContentItemID
				WHERE (( CI.ModuleId = @ModuleId ) OR ( CI.ModuleId IS NULL ))
				  AND (( @GroupId < 1 AND (BB.GroupId < 1 OR BB.GroupId IS NULL)) OR ( BB.GroupId = @GroupId ))
			)
	  AND (@ShowUnauthorized = 1 OR (@ShowUnauthorized = 0 AND B.Authorized = 1))

    ORDER BY Title ASC
END
GO

/********************************************************
 * SPROC: Blogs_Entry_Search
 ********************************************************/
IF OBJECT_ID(N'{databaseOwner}[{objectQualifier}Blogs_Entry_Search]', N'P') IS NULL
	EXEC('CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_Search] AS BEGIN SELECT 1 END');
GO

ALTER PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_Search
	@UserID INT ,
	@ModuleId INT ,
	@BlogID INT ,
	@StartDate DATETIME ,
	@EndDate DATETIME ,
	@Tags NVARCHAR(1024) ,
	@PageSize INT ,
	@PageIndex INT ,
	@ShowUnpublished BIT ,
	@Filter INT ,
	@GroupId INT ,
	@AuthorId INT  = -1
AS
BEGIN
	DECLARE @Today datetime = getutcdate();
	DECLARE @TitleMetaDataID int =
		(SELECT TOP 1 MetaDataID FROM {databaseOwner}[{objectQualifier}MetaData] WITH (NOLOCK) WHERE MetaDataName = 'Title');

	DECLARE @tblResult TABLE (RowNumber INT, TotalRecords INT, EntryId INT)
	DECLARE @tblTaxonomy TABLE (ContentItemID INT);
	DECLARE @tblComments TABLE (ContentItemID INT);

	--extract the ContentItems matching "my comments"
	IF @Filter=2
	BEGIN
		INSERT INTO @tblComments(ContentItemID)
		SELECT j.ContentItemId FROM {databaseOwner}{objectQualifier}Journal AS j WITH (NOLOCK)
			INNER JOIN {databaseOwner}{objectQualifier}Journal_Comments AS jc WITH (NOLOCK) ON jc.JournalId = j.JournalId AND jc.UserId = @UserID
	END

	--If tags, match the tags on ContentItems
	IF @Tags IS NOT NULL AND LEN(@Tags)> 0
	BEGIN
		INSERT INTO @tblTaxonomy(ContentItemID)
		SELECT CIT.ContentItemID from {databaseOwner}[{objectQualifier}ContentItems_Tags] CIT WITH (NOLOCK)
			INNER JOIN {databaseOwner}[{objectQualifier}Taxonomy_Terms] TT with (NOLOCK) ON CIT.TermID = TT.TermID
			INNER JOIN {databaseOwner}[{objectQualifier}ConvertListToTable](',', @Tags) T ON TT.Name = T.RowValue

		IF (SELECT COUNT(*) FROM @tblTaxonomy) > 0 --There are content items to match the tags
		BEGIN
			WITH EntrySet
			AS ( SELECT ROW_NUMBER() OVER ( ORDER BY E.Pinned DESC, E.PublishOnDate DESC ) AS RowNumber ,
						COUNT(*) OVER () AS [TotalRecords],
						E.EntryId

				FROM    {databaseOwner}{objectQualifier}Blogs_Blog B WITH (NOLOCK)
							INNER JOIN {databaseOwner}{objectQualifier}Blogs_Entry E WITH (NOLOCK) ON B.BlogId = E.BlogId
							INNER JOIN @tblTaxonomy X ON X.ContentItemId = E.ContentItemId
							INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI WITH (NOLOCK) ON E.ContentItemId = CI.ContentItemID

				WHERE   CI.ModuleID = @ModuleId
						AND ( @GroupId < 1 OR B.GroupId = @GroupId )
						AND
						(
						  -- filter by date range
							(@Filter = 0 AND E.PublishOnDate BETWEEN @StartDate AND @EndDate)
						  OR
						  -- filter by blog
							(@Filter = 1 AND B.BlogId = @BlogId)
						  OR
						  -- filter by my comments
							(@Filter = 2 AND E.ContentItemId IN (SELECT ContentItemID FROM @tblComments))
						  OR
						  -- filter by author
							(@Filter = 3 AND ((CI.CreatedByUserID = @AuthorId AND B.AuthorMode = 2) OR (B.UserId = @AuthorId AND B.AuthorMode IN (0,1))))
						  OR
						  -- filter by my blog entries
							(@Filter = 4 AND B.BlogId IN (SELECT BlogId FROM {databaseOwner}{objectQualifier}Blogs_Blog BL WITH (NOLOCK) WHERE BL.UserId = @UserID))
						   OR
						  -- filter by blog and date range
							(@Filter = 5 AND B.BlogId = @BlogId AND E.PublishOnDate BETWEEN @StartDate AND @EndDate)
						)
						AND ( @ShowUnpublished = 1 OR E.PublishOnDate > @Today OR (Authorized = 1 AND E.Approved = 1) )
				)
				INSERT INTO @tblResult(RowNumber, Totalrecords, EntryId)
				SELECT * FROM EntrySet
				WHERE   RowNumber BETWEEN ( @PageIndex * @PageSize + 1 ) AND  ( ( @PageIndex + 1 ) * @PageSize )
		END
	END
	ELSE
	BEGIN
		WITH  EntrySet
		AS ( SELECT ROW_NUMBER() OVER ( ORDER BY E.Pinned DESC, E.PublishOnDate DESC ) AS RowNumber ,
					COUNT(*) OVER () AS [TotalRecords],
					E.EntryId

			FROM    {databaseOwner}{objectQualifier}Blogs_Blog B WITH (NOLOCK)
						INNER JOIN {databaseOwner}{objectQualifier}Blogs_Entry E WITH (NOLOCK) ON B.BlogId = E.BlogId
						INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI WITH (NOLOCK) ON E.ContentItemId = CI.ContentItemID

			WHERE    CI.ModuleID = @ModuleId
					AND ( @GroupId < 1 OR B.GroupId = @GroupId )
					AND ( @ShowUnpublished = 1 OR E.PublishOnDate > @Today OR (Authorized = 1 AND E.Approved = 1) )
					AND
					(
						-- filter by date range
						(@Filter = 0 AND E.PublishOnDate BETWEEN @StartDate AND @EndDate)
					  OR
						-- filter by blog
						(@Filter = 1 AND B.BlogId = @BlogId)
					  OR
						-- filter by my comments
						(@Filter = 2 AND E.ContentItemId IN (SELECT ContentItemID FROM @tblComments))
					  OR
						-- filter by author
						(@Filter = 3 AND ((CI.CreatedByUserID = @AuthorId AND B.AuthorMode = 2) OR (B.UserId = @AuthorId AND B.AuthorMode IN (0,1))))
					  OR
						-- filter by my blog entries
						(@Filter = 4 AND B.BlogId IN (SELECT BlogId FROM {databaseOwner}{objectQualifier}Blogs_Blog BL WITH (NOLOCK) WHERE BL.UserId = @UserID))
					  OR
						-- filter by blog and date range
						(@Filter = 5 AND B.BlogId = @BlogId AND E.PublishOnDate BETWEEN @StartDate AND @EndDate)
					)
		)

		INSERT INTO @tblResult(RowNumber, Totalrecords, EntryId)
		SELECT * FROM EntrySet
		WHERE RowNumber BETWEEN ( @PageIndex * @PageSize + 1 ) AND ( ( @PageIndex + 1 ) * @PageSize )
	END

	-- extract all data for the page selected
	SELECT	T.RowNumber,
			T.TotalRecords,
			E.* ,
			B.PortalId ,
			B.UserId ,
			B.Authorized ,
			B.GroupId,
			B.AuthorMode,
			CI.Content ,
			CI.ContentTypeID ,
			CI.TabID ,
			CI.ModuleID ,
			CI.ContentKey ,
			CI.Indexed ,
			CI.CreatedByUserID ,
			CI.[CreatedOnDate] ,
			CI.LastModifiedByUserID ,
			CI.[LastModifiedOnDate],
			CIMD.[MetaDataValue] AS ContentTitle

	FROM @tblResult T
			INNER JOIN {databaseOwner}{objectQualifier}Blogs_Entry E WITH (NOLOCK) ON T.EntryId = E.EntryId
			INNER JOIN {databaseOwner}{objectQualifier}Blogs_Blog B WITH (NOLOCK) ON B.BlogId = E.BlogId
			INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI WITH (NOLOCK) ON E.ContentItemId = CI.ContentItemID
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}ContentItems_MetaData CIMD WITH (NOLOCK)
					ON CI.[ContentItemID] = CIMD.ContentItemID AND CIMD.MetaDataID = @TitleMetaDataID

END
GO

IF OBJECT_ID(N'{databaseOwner}[{objectQualifier}Blogs_Entry_GetByContentItem]', N'P') IS NULL
	EXEC('CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_GetByContentItem] AS BEGIN SELECT 1 END');
GO

ALTER PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_GetByContentItem
	@ContentItemId INT,
	@ModuleId INT
AS
BEGIN
	SELECT  E.*,
			B.UserId ,
			B.AuthorMode ,
			B.PortalId,
			B.GroupId,
			U.UserName ,
			U.DisplayName ,
			CI.CreatedByUserID ,
			CI.CreatedOnDate ,
			CI.ContentKey ,
			CI.Indexed ,
			CI.Content ,
			CI.ContentItemID ,
			CI.LastModifiedByUserID ,
			CI.LastModifiedOnDate ,
			CI.ModuleID ,
			CI.TabID ,
			CI.ContentTypeID ,
			CIMD.[MetaDataValue] AS [ContentTitle] ,
			( SELECT    1
			) AS TotalRecords
	FROM    {databaseOwner}{objectQualifier}Blogs_Entry E WITH(NOLOCK)
				INNER JOIN {databaseOwner}{objectQualifier}Blogs_Blog B WITH(NOLOCK) ON B.BlogId = E.BlogId
				INNER JOIN {databaseOwner}{objectQualifier}Users U WITH(NOLOCK) ON U.UserID = B.UserId
				INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI WITH(NOLOCK) ON E.ContentItemId = CI.ContentItemID
				LEFT OUTER JOIN {databaseOwner}{objectQualifier}ContentItems_MetaData CIMD WITH(NOLOCK)
														ON	CIMD.ContentItemID = E.ContentItemId
															AND CIMD.MetaDataID = ( SELECT
																					MetaDataID
																					FROM
																					{databaseOwner}{objectQualifier}MetaData
																					WHERE
																					MetaDataName = 'Title'
																				  )
	WHERE   E.ContentItemId = @ContentItemId
	AND		CI.ModuleID = @ModuleId
END
GO

/* Update mechanics action types (for upgrades) */
DECLARE @DesktopModuleId INT
SET @DesktopModuleId = ( SELECT DesktopModuleID FROM {databaseOwner}[{objectQualifier}DesktopModules] WHERE  ModuleName = 'Blogs' AND FolderName = 'DNNCorp/Blogs')

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 9
	WHERE ActionName = 'ApprovedBlogEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 7
	WHERE ActionName = 'BookmarkedBlogEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 9
	WHERE ActionName = 'DeletedBlogEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 9
	WHERE ActionName = 'DeletedComment' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 7
	WHERE ActionName = 'SubscribedToAllBlogs' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 3
	WHERE ActionName = 'PostedComment' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 7
	WHERE ActionName = 'SubscribedToEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 8
	WHERE ActionName = 'UnsubscribedFromEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 8
	WHERE ActionName = 'UnsubscribedFromAllBlogs' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 9
	WHERE ActionName = 'ApprovedBlogComment' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 7
	WHERE ActionName = 'LikedBlogEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 8
	WHERE ActionName = 'UnLikedBlogEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 7
	WHERE ActionName = 'LikedBlogEntryComment' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 8
	WHERE ActionName = 'UnLikedBlogEntryComment' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 12
	WHERE ActionName = 'ProvidedLikedBlogEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 13
	WHERE ActionName = 'ProvidedUnLikedBlogEntry' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 12
	WHERE ActionName = 'ProvidedLikedBlogEntryComment' AND DesktopModuleId = @DesktopModuleId

UPDATE {databaseOwner}[{objectQualifier}Mechanics_ScoringActionDefinition]
	SET ActionType = 13
	WHERE ActionName = 'ProvidedUnLikedBlogEntryComment' AND DesktopModuleId = @DesktopModuleId
GO

/********************************************************
 * SPROC: Blogs_Entry_Get
 ********************************************************/
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}{objectQualifier}Blogs_Entry_Get') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_Get
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_Get]
	@EntryId INT,
	@PortalId INT
AS
BEGIN
	SELECT  E.*,
			B.UserId,
			B.AuthorMode,
			U.UserName,
			U.DisplayName,
			CI.CreatedByUserID,
			CI.CreatedOnDate,
			CI.ContentKey,
			CI.Indexed,
			CI.Content,
			CI.ContentItemID,
			CI.LastModifiedByUserID,
			CI.LastModifiedOnDate,
			CI.ModuleID,
			CI.TabID,
			CI.ContentTypeID,
			@PortalID AS PortalId,
			( SELECT    1
			) AS TotalRecords
	FROM    {databaseOwner}{objectQualifier}Blogs_Entry E
			INNER JOIN {databaseOwner}{objectQualifier}Blogs_Blog B ON B.BlogId = E.BlogId
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users U ON U.UserID = B.UserId
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}ContentItems CI ON E.ContentItemId = CI.ContentItemID
	WHERE   E.EntryId = @EntryId
			AND B.PortalId = @PortalId
END
GO

/********************************************************
 * SPROC: Blogs_Blog_Get
 ********************************************************/
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}{objectQualifier}Blogs_Blog_Get') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}Blogs_Blog_Get
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Blog_Get]
    @BlogId INT
AS
BEGIN
	SELECT  B.*,
			U.[UserName],
			U.[DisplayName],
			( SELECT    COUNT(BlogId)
			  FROM      {databaseOwner}{objectQualifier}Blogs_Entry E
			  WHERE     E.BlogId = B.BlogId
						AND E.Approved = 1 AND E.PublishOnDate <= GETUTCDATE()
			) AS BlogPostCount
	FROM    {databaseOwner}{objectQualifier}Blogs_Blog B
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users U ON B.[UserId] = U.[UserID]
	WHERE   BlogId = @BlogId
END
GO

/********************************************************
 * SPROC: Blogs_Blog_GetByPortal
 ********************************************************/
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}{objectQualifier}Blogs_Blog_GetByPortal') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}Blogs_Blog_GetByPortal
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Blog_GetByPortal]
    @PortalId INT
AS
BEGIN
	SELECT  B.*,
			U.[UserName],
			U.[DisplayName],
			( SELECT    COUNT(BlogId)
			  FROM      {databaseOwner}{objectQualifier}Blogs_Entry E
			  WHERE     E.BlogId = B.BlogId
						AND E.Approved = 1 AND E.PublishOnDate <= GETUTCDATE()
			) AS BlogPostCount
	FROM    {databaseOwner}{objectQualifier}Blogs_Blog B
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users U ON B.[UserId] = U.[UserID]
	WHERE   PortalId = @PortalId
	ORDER BY Title
END
GO

/********************************************************
 * SPROC: Blogs_Entry_Add
 ********************************************************/
IF OBJECT_ID(N'{databaseOwner}[{objectQualifier}Blogs_Entry_Add]', N'P') IS NULL
	EXEC('CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_Add] AS BEGIN SELECT 1 END');
GO

ALTER PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_Add]
	@BlogId INT,
	@Title NVARCHAR(255),
	@Summary NVARCHAR(MAX),
	@Entry NVARCHAR(MAX),
	@Approved BIT,
	@AllowComments BIT,
	@Pinned BIT,
	@PermaLink NVARCHAR(1024),
	@ContentItemId INT,
	@PublishOnDate DATETIME
AS
BEGIN
	INSERT  INTO {databaseOwner}{objectQualifier}Blogs_Entry
			( [BlogId],
			  [Title],
			  [Summary],
			  [Entry],
			  [Approved],
			  [AllowComments],
			  [Pinned],
			  [PermaLink],
			  [ContentItemId],
			  [Views],
			  [PublishOnDate]
			)
	VALUES  ( @BlogID,
			  @Title,
			  @Summary,
			  @Entry,
			  @Approved,
			  @AllowComments,
			  @Pinned,
			  NULL,
			  @ContentItemId,
			  0,
			  @PublishOnDate
			)

	DECLARE @EntryID INT = SCOPE_IDENTITY()

	UPDATE  {databaseOwner}{objectQualifier}Blogs_Blog
	SET     LastEntry = GETUTCDATE()
	WHERE   BlogId = @BlogId

	IF NOT @PermaLink IS NULL
		UPDATE  {databaseOwner}{objectQualifier}Blogs_Entry
		SET     PermaLink = @PermaLink + CONVERT(NVARCHAR(10), EntryId)
		WHERE   BlogId = @BlogId
		  AND   PermaLink IS NULL

	SELECT  @EntryId
END
GO

/********************************************************
 * SPROC: Blogs_Entry_GetAllByBlog
 ********************************************************/
IF EXISTS (SELECT * FROM {databaseOwner}SYSOBJECTS WHERE id = object_id(N'{databaseOwner}[{objectQualifier}Blogs_Entry_GetAllByBlog]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_GetAllByBlog]
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_GetAllByBlog
 	@BlogId INT
AS 
BEGIN
	SELECT  E.*,
			B.PortalId,
			B.UserId,
			B.Authorized,
			B.GroupId,
			B.AuthorMode,
			CI.Content,
			CI.ContentTypeID,
			CI.TabID,
			CI.ModuleID ,
			CI.ContentKey ,
			CI.Indexed ,
			CI.CreatedByUserID ,
			CI.[CreatedOnDate] ,
			CI.LastModifiedByUserID ,
			CI.[LastModifiedOnDate]
	FROM    {databaseOwner}{objectQualifier}Blogs_Blog B
			INNER JOIN {databaseOwner}{objectQualifier}Blogs_Entry E ON B.BlogId = E.BlogId
			INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI ON E.ContentItemId = CI.ContentItemID
	
	WHERE  B.BlogId = @BlogId
END
GO

/********************************************************
 * SPROC: Blogs_Entry_GetByContentItem
 ********************************************************/
IF EXISTS (SELECT * FROM {databaseOwner}SYSOBJECTS WHERE id = object_id(N'{databaseOwner}[{objectQualifier}Blogs_Entry_GetByContentItem]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_GetByContentItem]
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_GetByContentItem
	@ContentItemId INT,
	@PortalId INT
AS 
BEGIN
	SELECT  E.*,
			B.UserId ,
			B.AuthorMode ,
			U.UserName ,
			U.DisplayName ,
			CI.CreatedByUserID ,
			CI.CreatedOnDate ,
			CI.ContentKey ,
			CI.Indexed ,
			CI.Content ,
			CI.ContentItemID ,
			CI.LastModifiedByUserID ,
			CI.LastModifiedOnDate ,
			CI.ModuleID ,
			CI.TabID ,
			CI.ContentTypeID ,
			@PortalID AS PortalId,
			CIMD.[MetaDataValue] AS [ContentTitle] ,
			( SELECT    1
			) AS TotalRecords
	FROM    {databaseOwner}{objectQualifier}Blogs_Entry E
			INNER JOIN {databaseOwner}{objectQualifier}Blogs_Blog B ON B.BlogId = E.BlogId
			INNER JOIN {databaseOwner}{objectQualifier}Users U ON U.UserID = B.UserId
			INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI ON E.ContentItemId = CI.ContentItemID
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}ContentItems_MetaData CIMD ON CIMD.ContentItemID = E.ContentItemId
															  AND CIMD.MetaDataID = ( SELECT
															  MetaDataID
															  FROM
															  {databaseOwner}{objectQualifier}MetaData
															  WHERE
															  MetaDataName = 'Title'
															  )
	WHERE   E.ContentItemId = @ContentItemId
	AND		PortalId = @PortalId
END
GO

/********************************************************
 * SPROC: Blogs_Entry_GetByDates
 ********************************************************/
IF EXISTS (SELECT * FROM {databaseOwner}SYSOBJECTS WHERE id = object_id(N'{databaseOwner}[{objectQualifier}Blogs_Entry_GetByDates]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_GetByDates]
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_GetByDates
	@BlogID INT,
	@PortalId INT,
	@GroupId INT,
	@StartDate DATETIME,
	@AuthorId INT  = -1
AS 
	BEGIN

		SELECT  BE.*, B.AuthorMode
		FROM    {databaseOwner}{objectQualifier}Blogs_Entry BE
		INNER JOIN {databaseOwner}{objectQualifier}Blogs_Blog B
			ON BE.BlogId = B.BlogId
		INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI 
			ON BE.ContentItemId = CI.ContentItemID
		WHERE B.PortalId = @PortalId
			AND ((BE.BlogID = @BlogID AND @BlogID > 0) OR (BE.BlogID > 0 AND @BlogID <= 0))
			AND ((B.GroupID = @GroupId) OR (B.GroupID is NULL AND @GroupId < 0))
			AND (@AuthorId = -1 OR CI.CreatedByUserID = @AuthorId)
			AND BE.Approved = 1
			AND B.Authorized = 1
			AND BE.PublishOnDate BETWEEN @StartDate AND GetUtcDate()
	END
GO

/********************************************************
 * SPROC: Blogs_Entry_GetByRange
 ********************************************************/
IF EXISTS (SELECT * FROM {databaseOwner}SYSOBJECTS WHERE id = object_id(N'{databaseOwner}[{objectQualifier}Blogs_Entry_GetByRange]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_GetByRange]
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_GetByRange
    @StartingEntryId INT,	
	@RecordsToReturn INT
AS
BEGIN
    SELECT  TOP (@RecordsToReturn) BE.EntryId, B.PortalId, B.GroupId, B.AuthorMode
    FROM    {databaseOwner}{objectQualifier}Blogs_Entry BE
	INNER JOIN {databaseOwner}{objectQualifier}Blogs_Blog B
		ON BE.BlogId = B.BlogId
    WHERE   
		EntryId > @StartingEntryId    
    ORDER BY EntryId ASC
END
GO

/********************************************************
 * SPROC: Blogs_Entry_GetSearchable
 ********************************************************/
IF OBJECT_ID(N'{databaseOwner}[{objectQualifier}Blogs_Entry_GetSearchable]', N'P') IS NOT NULL
	DROP PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_GetSearchable
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_GetSearchable]
	@ModuleID INT,
	@StartDate DATETIME,
	@EndDate DATETIME,
	@StartDateUTC DATETIME,
	@EndDateUTC DATETIME
AS
	SELECT  E.* ,
			B.UserId ,
			B.PortalId,
			B.AuthorMode,
			(CASE B.GroupId WHEN NULL THEN -1 ELSE B.GroupId END) AS [GroupId],
			CI.CreatedByUserID ,
			CI.CreatedOnDate ,
			CI.ContentKey ,
			CI.Indexed ,
			CI.Content ,
			CI.ContentItemID ,
			CI.LastModifiedByUserID ,
			CI.LastModifiedOnDate ,
			CI.ModuleID ,
			CI.TabID ,
			CI.ContentTypeID ,
			COUNT(*) OVER () AS TotalRecords,
			CIMD.[MetaDataValue] AS [ContentTitle]
	FROM    {databaseOwner}{objectQualifier}Blogs_Entry E
			INNER JOIN {databaseOwner}{objectQualifier}Blogs_Blog B ON B.BlogId = E.BlogId
			INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI ON E.ContentItemId = CI.ContentItemID
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}ContentItems_MetaData CIMD ON CIMD.ContentItemID = E.ContentItemId
				AND CIMD.MetaDataID = (SELECT MetaDataID FROM {databaseOwner}[{objectQualifier}MetaData] WHERE MetaDataName = 'Title')
	WHERE   CI.ModuleID = @ModuleID
			AND B.Authorized = 1
			AND
			(
			  (LastModifiedOnDate >= @StartDate AND LastModifiedOnDate <= @EndDate 
					AND E.PublishOnDate <= @EndDateUTC)
			  OR 
			  (
				E.PublishOnDate >= @StartDateUTC AND E.PublishOnDate <= @EndDateUTC 
			  )
			  OR
			  (										
			    (SELECT COUNT(*) FROM {databaseOwner}{objectQualifier}Journal_Comments JC 
				 WHERE JC.[JournalId] IN (SELECT JournalId FROM {databaseOwner}{objectQualifier}Journal J WHERE J.[ContentItemId] = E.[ContentItemId]) 
				  AND 
				  ((JC.[DateCreated] >= @StartDateUTC AND JC.[DateCreated] <= @EndDateUTC)
				    OR
				  (JC.[DateUpdated] >= @StartDateUTC AND JC.[DateUpdated] <= @EndDateUTC)
				  )
				) > 0 
			  )
			)

	ORDER BY E.Pinned DESC
GO

/********************************************************
 * SPROC: Blogs_Entry_Search
 ********************************************************/
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}{objectQualifier}Blogs_Entry_Search') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_Search
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_Search
	@UserID INT,
	@ModuleId INT ,
	@BlogID INT ,
	@StartDate DATETIME ,
	@EndDate DATETIME ,
	@Tags NVARCHAR(1024) ,
	@PageSize INT ,
	@PageIndex INT ,
	@ShowUnpublished BIT ,
	@Filter INT,
	@GroupId INT,
	@AuthorId INT  = -1
AS 
	BEGIN

		WITH    EntrySet
				  AS ( SELECT   ROW_NUMBER() OVER ( ORDER BY E.Pinned DESC, E.PublishOnDate DESC ) AS RowNumber ,
								COUNT(*) OVER () AS [TotalRecords],
								E.* ,
								B.PortalId ,
								B.UserId ,
								B.Authorized ,
								B.GroupId,
								B.AuthorMode,
								CI.Content ,
								CI.ContentTypeID ,
								CI.TabID ,
								CI.ModuleID ,
								CI.ContentKey ,
								CI.Indexed ,
								CI.CreatedByUserID ,
								CI.[CreatedOnDate] ,
								CI.LastModifiedByUserID ,
								CI.[LastModifiedOnDate] ,
								CIMD.[MetaDataValue] AS [ContentTitle]
					   FROM     {databaseOwner}{objectQualifier}Blogs_Blog B
								INNER JOIN {databaseOwner}{objectQualifier}Blogs_Entry E ON B.BlogId = E.BlogId
								INNER JOIN {databaseOwner}{objectQualifier}ContentItems CI ON E.ContentItemId = CI.ContentItemID
								LEFT OUTER JOIN {databaseOwner}[{objectQualifier}ContentItems_MetaData] CIMD 
									ON CIMD.ContentItemID = e.[ContentItemID]
										AND CIMD.MetaDataID = 
										(SELECT MetaDataID FROM {databaseOwner}[{objectQualifier}MetaData] WHERE MetaDataName = 'Title')

					   WHERE    CI.ModuleID = @ModuleId
								AND ( @GroupId < 1 OR B.GroupId = @GroupId)
								AND ( @BlogID < 1 OR B.BlogId = @BlogID)
								
								AND 
								(
									(@Filter = 0 AND E.PublishOnDate BETWEEN @StartDate AND @EndDate) 
										OR
									(@Filter = 1 AND B.BlogId IN (SELECT BlogId FROM {databaseOwner}{objectQualifier}Blogs_Blog BL WHERE BL.UserId = @UserID)
									 ) 
										OR
									(@Filter = 2 AND EXISTS (SELECT TOP 1
																	e.EntryId,
																	j.ContentItemId ,
																	j.ObjectKey ,
																	jc.CommentId ,
																	jc.UserId
																	FROM {databaseOwner}{objectQualifier}Journal_Comments AS jc
																	INNER JOIN {databaseOwner}{objectQualifier}Journal AS j ON jc.JournalId = j.JournalId
																	INNER JOIN {databaseOwner}{objectQualifier}Blogs_Entry AS e ON j.ContentItemId = e.ContentItemId
																WHERE   E.ContentItemId = CI.ContentItemID AND jc.UserId = @UserID
															)
									)
										OR
									(@Filter = 3 AND CI.CreatedByUserID = @AuthorId
									 ) 
								)
								
								
								AND (@ShowUnpublished = 1 OR (E.PublishOnDate > getutcdate() OR (Authorized = 1 AND E.Approved = 1)))

								AND 
								(
									(@Tags IS NULL OR LEN(@Tags) = 0) 
									
									OR
																
									(SELECT COUNT(*) FROM {databaseOwner}[{objectQualifier}ContentItems_Tags] CIT
										INNER JOIN {databaseOwner}[{objectQualifier}Taxonomy_Terms] TT ON CIT.TermID = TT.TermID
										INNER JOIN {databaseOwner}[{objectQualifier}ConvertListToTable](',', @Tags) T ON TT.Name = T.RowValue
										WHERE CIT.ContentItemID = E.ContentItemId
									) = (SELECT LEN(@Tags) - LEN(REPLACE(@Tags, ',', ''))+ 1)
								)
						)
			SELECT  *
			FROM    EntrySet
			WHERE   RowNumber BETWEEN ( @PageIndex * @PageSize + 1 )
							  AND     ( ( @PageIndex + 1 ) * @PageSize )		
	END
GO

/********************************************************
 * SPROC: Blogs_Entry_GetByUserId
 ********************************************************/
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}{objectQualifier}Blogs_Entry_GetByUserId') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}Blogs_Entry_GetByUserId
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Entry_GetByUserId]
	@PortalId INT,
	@UserId INT	
AS
BEGIN
	SELECT  E.*,
			B.UserId,
			B.AuthorMode,
			U.UserName,
			U.DisplayName,
			CI.CreatedByUserID,
			CI.CreatedOnDate,
			CI.ContentKey,
			CI.Indexed,
			CI.Content,
			CI.ContentItemID,
			CI.LastModifiedByUserID,
			CI.LastModifiedOnDate,
			CI.ModuleID,
			CI.TabID,
			CI.ContentTypeID,
			@PortalID AS PortalId,
			( SELECT    1
			) AS TotalRecords
	FROM    {databaseOwner}{objectQualifier}Blogs_Entry E
			INNER JOIN {databaseOwner}{objectQualifier}Blogs_Blog B ON B.BlogId = E.BlogId
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users U ON U.UserID = B.UserId
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}ContentItems CI ON E.ContentItemId = CI.ContentItemID
	WHERE   B.PortalId = @PortalId
			AND CI.CreatedByUserID = @UserId
END
GO

/********************************************************
 * SPROC: Blogs_Blog_GetByUserId
 ********************************************************/
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}{objectQualifier}Blogs_Blog_GetByUserId') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}Blogs_Blog_GetByUserId
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Blogs_Blog_GetByUserId]
    @PortalId INT,
	@UserId INT
AS
BEGIN
	SELECT  B.*,
			U.[UserName],
			U.[DisplayName],
			( SELECT    COUNT(BlogId)
			  FROM      {databaseOwner}{objectQualifier}Blogs_Entry E
			  WHERE     E.BlogId = B.BlogId
						AND E.Approved = 1 AND E.PublishOnDate <= GETUTCDATE()
			) AS BlogPostCount
	FROM    {databaseOwner}{objectQualifier}Blogs_Blog B
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users U ON B.[UserId] = U.[UserID]
	WHERE   PortalId = @PortalId AND U.UserId = @UserId
	ORDER BY Title
END
GO
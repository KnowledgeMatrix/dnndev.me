IF object_id(N'{databaseOwner}{objectQualifier}Social_Slug', 'U') IS NOT NULL
BEGIN
	ALTER TABLE {databaseOwner}{objectQualifier}ContentItem_Slug NOCHECK CONSTRAINT ALL

	INSERT {databaseOwner}{objectQualifier}ContentItem_Slug (TabId, ContentItemId, HttpStatus, Slug)
	SELECT TabId, ContentItemId, HttpStatus, Slug
	FROM {databaseOwner}{objectQualifier}Social_Slug

	ALTER TABLE {databaseOwner}{objectQualifier}ContentItem_Slug CHECK CONSTRAINT ALL

	DROP TABLE {databaseOwner}{objectQualifier}Social_Slug
END
GO

DECLARE @OldId INT = (SELECT ExtensionUrlProviderID FROM  {databaseOwner}{objectQualifier}ExtensionUrlProviders p
						WHERE p.ProviderName = 'DNN Social Url Extension Provider')
IF @OldId IS NOT NULL
BEGIN
	UPDATE {databaseOwner}{objectQualifier}ExtensionUrlProviderSetting 
	SET ExtensionUrlProviderID = (SELECT ExtensionUrlProviderID FROM {databaseOwner}{objectQualifier}ExtensionUrlProviders p
							WHERE p.ProviderName = 'Evoq Content Item Url Extension Provider')
	WHERE ExtensionUrlProviderID = @OldId

	DELETE FROM {databaseOwner}{objectQualifier}ExtensionUrlProviders WHERE ExtensionUrlProviderID = @OldId

	DELETE FROM {databaseOwner}{objectQualifier}ExtensionUrlProviderConfiguration WHERE ExtensionUrlProviderID = @OldId

	DELETE FROM {databaseOwner}{objectQualifier}ExtensionUrlProviderTab WHERE ExtensionUrlProviderID = @OldId
END

IF object_id(N'{databaseOwner}{objectQualifier}Social_ResolveSlug', 'P') IS NOT NULL
	DROP PROCEDURE {databaseOwner}{objectQualifier}Social_ResolveSlug
GO

IF object_id(N'{databaseOwner}{objectQualifier}Social_AddSlugToContentItem', 'P') IS NOT NULL
	DROP PROCEDURE {databaseOwner}{objectQualifier}Social_AddSlugToContentItem
GO

IF object_id(N'{databaseOwner}{objectQualifier}Social_GetSlug', 'P') IS NOT NULL
	DROP PROCEDURE {databaseOwner}{objectQualifier}Social_GetSlug
GO

UPDATE {databaseOwner}{objectQualifier}PortalSettings 
SET SettingName = 'ContentItemUrlProvider_ExcludeTabIds'
WHERE SettingName = 'SocialUrls_ExcludeTabIds'
GO

IF object_id(N'{databaseOwner}{objectQualifier}Social_Group_GetGroup', 'P') IS NOT NULL
	DROP PROCEDURE {databaseOwner}{objectQualifier}Social_Group_GetGroup
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Social_Group_GetGroup]
	@PortalId INT,
	@GroupId INT,
	@UserId INT,
	@AllowAnonymous BIT
AS
BEGIN
	;WITH AllowedRoles AS
	(
		SELECT RoleId FROM {databaseOwner}{objectQualifier}Roles 
		WHERE PortalId = @PortalId AND IsSystemRole = 1 AND RoleName IN 
		('Administrators','Community Manager', 'Moderators', 'Content Editors','Content Managers')
	)
	
	SELECT	R.PortalID As PortalId,
			R.RoleID AS GroupId,
			R.RoleName AS GroupName,
			R.Description,
			R.IsPublic,
			R.IconFile,
			R.Status,
			R.RoleGroupID,
			R.SecurityMode,
			R.IsSystemRole,
			R.CreatedByUserID,
			R.CreatedOnDate,
			R.LastModifiedOnDate,
			(SELECT CAST ((SELECT RoleId FROM {databaseOwner}{objectQualifier}UserRoles WITH (NOLOCK) WHERE RoleId = @GroupId AND UserId = @UserId ) AS BIT)) AS IsMember,

			(SELECT CAST ((SELECT RoleId FROM {databaseOwner}{objectQualifier}UserRoles WITH (NOLOCK) WHERE RoleId = @GroupId AND UserId = @UserId AND Status = -1) AS BIT)) AS MembershipPending,

			(SELECT CAST ((SELECT SettingValue FROM {databaseOwner}{objectQualifier}RoleSettings WHERE RoleId = R.RoleId AND SettingName = 'ReviewMembers') AS BIT)) AS ReviewMembers,

			(SELECT COUNT(*) FROM {databaseOwner}{objectQualifier}UserRoles U 
				INNER JOIN {databaseOwner}{objectQualifier}UserPortals UP 
				ON UP.UserId = U.UserId
				WHERE U.RoleID = R.RoleID AND UP.PortalId = @PortalId AND U.Status = 1 AND UP.IsDeleted = 0) AS MemberCount
	FROM    
			{databaseOwner}{objectQualifier}Roles AS R			
	WHERE
			R.RoleId = @GroupId
			AND
			R.SecurityMode IN (1, 2)
			AND (	R.IsPublic = 1
					OR
					-- Super User
					(EXISTS (SELECT UserID FROM {databaseOwner}{objectQualifier}Users WHERE @UserId = UserID AND IsSuperUser = 1))
					OR
					-- Admin
					(EXISTS (SELECT UserID FROM {databaseOwner}{objectQualifier}UserRoles WHERE @UserId = UserID AND RoleId IN 
						(SELECT RoleId FROM AllowedRoles)))			
					OR
					-- Member
					(IsPublic = 0 AND EXISTS (SELECT RoleID FROM {databaseOwner}{objectQualifier}UserRoles WHERE R.RoleID = RoleID AND @UserId = UserID))
					OR
					-- Private Anonymous Viewing
					IsPublic = 0 AND @AllowAnonymous = 1
				)
							
END
GO